apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

sourceCompatibility = 1.8
version = '1.0'
jar {
    manifest {
        attributes 'Implementation-Title': 'EAMapping',
        'Implementation-Version': version,
        'Bundle-Name': 'EAMapping',
        'Bundle-Version': version
    }
}

repositories { mavenCentral() }

dependencies {
    compile 'org.hibernate:hibernate-core:[5.0.0-Beta2,)'
    compile 'org.hibernate:hibernate-entitymanager:[5.0.0-Beta2,)'
    compile 'org.hibernate:hibernate-c3p0:[5.0.0-Beta2,)'
    compile 'com.mchange:c3p0:0.9.5'
    compile 'org.postgresql:postgresql:9.4-1201-jdbc41'

    compile 'org.jboss.weld.se:weld-se-core:3.0.0.Alpha8'
    compile 'org.jboss:jandex:1.2.4.Final'

    compile 'org.slf4j:slf4j-api:[1.7.12,)'
    runtime 'ch.qos.logback:logback-classic:[1.1.3,)'
    runtime 'ch.qos.logback:logback-core:[1.1.3,)'

    compile 'org.jgrapht:jgrapht-core:0.9+'
    compile 'org.jgrapht:jgrapht-ext:0.9+'
    compile 'jgraph:jgraph:5.13.0.0'

    testCompile('org.jglue.cdi-unit:cdi-unit:3.1.2') {
        exclude group: 'org.jboss.weld.se'
    }
    testCompile 'org.reflections:reflections:0.9.10'  // v0.9.9-RC2 is broken, required by cdi-unit
    testRuntime 'com.h2database:h2:1.4.187'
    testCompile 'junit:junit:4.+'
}

uploadArchives {
    repositories { flatDir { dirs 'repos' } }
}

mainClassName = 'app.Main'

test {
    systemProperties['persistenceUnit'] = 'testPersistenceUnit'
}

/*
 * Make Gradle tag along with weld-se bean discovery.
 * See http://stackoverflow.com/q/30255760/27232
*/

task copyBeanDescriptor(type: Copy) {
    from('src/main/resources') { include 'META-INF/beans.xml' }
    into "$buildDir/classes/main/"
}

classes.dependsOn copyBeanDescriptor

sourceSets.main.resources {
    exclude 'META-INF/beans.xml'
}
