apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.8
version = '1.0'
jar {
    manifest {
        attributes 'Implementation-Title': 'EAMapping',
        'Implementation-Version': version,
        'Bundle-Name': 'EAMapping',
        'Bundle-Version': version
    }
}

dependencies {
    // Add all local JARs as dependencies
    compile fileTree(dir: 'libs', include: '**/*.jar')

    compile project(':EAMetamodel.model')
    compile project(':FaultTreeMetamodel.model')
    compile project(':utils')

    def hibernateVersion = "5.0.0.CR4"
    compile "org.hibernate:hibernate-core:$hibernateVersion"
    compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
    compile "org.hibernate:hibernate-c3p0:$hibernateVersion"
    compile 'com.mchange:c3p0:0.9.5.1'
    compile 'org.postgresql:postgresql:9.4-1201-jdbc41'

    compile 'org.jboss.weld.se:weld-se-core:3.0.0.Alpha12'
    compile 'org.jboss:jandex:2.0.0.Beta4'

    compile 'org.slf4j:slf4j-api:[1.7.12,)'
    runtime 'ch.qos.logback:logback-classic:[1.1.3,)'
    runtime 'ch.qos.logback:logback-core:[1.1.3,)'

    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'org.jgrapht:jgrapht-core:0.9+'
    compile 'org.jgrapht:jgrapht-ext:0.9+'
    compile 'jgraph:jgraph:5.13.0.0'

    def ecoreVersion = '2.11.0-v20150123-0347'
    compile "org.eclipse.emf:org.eclipse.emf.ecore:$ecoreVersion"
    compile "org.eclipse.emf:org.eclipse.emf.ecore.xmi:$ecoreVersion"
    compile "org.eclipse.emf:org.eclipse.emf.common:$ecoreVersion"
    compile 'org.eclipse.emf:org.eclipse.emf.texo:0.9.0-v201501182340'
    compile 'org.eclipse.core:runtime:3.10.0-v20140318-2214'

    testCompile('org.jglue.cdi-unit:cdi-unit:3.1.3') {
        exclude group: 'org.jboss.weld.se'
    }
    testCompile 'org.reflections:reflections:0.9.10'  // v0.9.9-RC2 is broken, required by cdi-unit
    testRuntime 'com.h2database:h2:1.4.188'
    testCompile 'junit:junit:4.+'
}

mainClassName = 'org.dslab.mdsd4sil.app.Bootstrap'

test {
    systemProperties['persistenceUnit'] = 'testPersistenceUnit'
}

/*
 * Make Gradle tag along with weld-se bean discovery.
 * See http://stackoverflow.com/q/30255760/27232
*/

task copyBeanDescriptor(type: Copy) {
    from('src/main/resources') { include 'META-INF/beans.xml' }
    into "$buildDir/classes/main/"
}

classes.dependsOn copyBeanDescriptor

sourceSets.main.resources {
    exclude 'META-INF/beans.xml'
}
